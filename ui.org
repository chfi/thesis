* User interface
The main function of GGB's user interface is to tie the browser tracks --
BD and Cy.js -- together. It also creates and to some extent manages
the JS browser track instances, and renders the HTML for the entire
UI.

To do all this, we use Halogen, from purescript-halogen, a type-safe
UI library, similar in purpose to React. Event passing between tracks
is taken care of by purescript-aff-bus and threads from purescript-aff,
and


** Quick Halogen intro
Halogen is a component-based UI library, using a virtual DOM implementation
to render HTML to the browser. A component is a value of the (fairly complicated)
type Component (removed constraints etc. for clarity):

#+BEGIN_SRC purescript
component :: Component renderer query state message monad
             (1)       (2)      (3)   (4)   (5)     (6)
#+END_SRC

The type `Component (1)` takes five type parameters. The first,
`renderer (2)` is the type used to render the page, we use a HTML
renderer. Next is `query (3)`, which is filled with our query algebra,
to be explained later; in short it is the set of commands the component
can respond to. `state (4)` is the type of the state kept by
the component. We don't have any, so we set it to `Unit`. `message (5)`
is the type of messages we can produce, which we can send to other
parts of the program.  Finally, `monad (6)` is the type in which all
effects produced by the component will happen. In our case, it's the
`Aff` monad for asynchronous effects -- it could also be a monad transformer
stack, or some free monad.

*** Query algebras

The "Query algebra" is the type describing the possible actions we can query
the component to perform. The type is not complicated; in GGB we have:

#+BEGIN_SRC purescript
data Query a
  = CreateBD (∀ eff. HTMLElement -> Eff (bd :: BD | eff) Biodalliance) a
  | PropagateMessage Message a
  | BDScroll Bp a
  | BDJump Chr Bp Bp a
  | CreateCy String a
  | ResetCy a
#+END_SRC

From top to bottom, we can ask it to `CreateBD`, providing a function that
creates a Biodalliance instance given a HTML element to place it in;
we can propagate messages from the child components; we can scroll and
jump the BD instance; and we can create and reset the Cy.js instance.
That's what the queries look like, but we also need to define an `eval`
function. This maps Query to Halogen commands, which are also defined
by a functor type -- the function is a natural transformation from
our Query DSL to the Halogen DSL (a free monad).

#+BEGIN_SRC purescript
eval :: Query ~> HalogenM state query childQuery childSlot message monad
#+END_SRC

The type parameters of `HalogenM` are the same as those of
`Component`, adding a `childQuery` type, the Query type of values
which this component can use to communicate with its children, and
`childSlot`, the type which is used to index into the child
components. For the main GGB component they are:

#+BEGIN_SRC purescript
type ChildSlot = Either2 UIBD.Slot UICy.Slot

type ChildQuery = Coproduct2 UIBD.Query UICy.Query
#+END_SRC

`ChildSlot` is a coproduct of the two child Slot /types/ (Either2) of the
child components; we can query the BD slot or the Cy.js slot at once,
not both. `Either2` is a generalization of `Either` to a variable
number of types, a convenience that makes it easy to change the
number of slots, without more work than a type synonym. `ChildQuery`
is a coproduct of the two child Query /functors/ (Coproduct2).

#+BEGIN_SRC purescript
data Either a b = Left a | Right b

data Coproduct f g a = Coproduct (Either (f a) (g a))
-- can be viewed as (pseudocode):
data Coproduct f g a = Coproduct (Left (f a)) | (Right (g a))

type ChildQuery a = Either (UIBD.Query a) (UICy.Query a)
#+END_SRC

-- TODO: not sure, but it may even be impossible to do this; may not compile
-- (certainly doesn't compile when applied to Halogen)

We can't use normal `Either` for ChildQuery, as we wouldn't be able to be
parametric over the `a` type in both child queries. If we were to map a function
`UICy.Query (a -> b)` on the Right component of the Either ChildQuery, we'd end
up with the type `Either (UIBD.Query a) (UICy.Query b)`, which obviously is not
congruent to `ChildQuery a`.

Writing the function is simple enough. We pattern match on the input
Query, and produce effects in the HalogenM type. Creating BD is done
by querying the BD child using its respective slot and a ChildPath --
a type describing a path to the child component, and providing an
action to tell the child component to perform.


#+BEGIN_SRC purescript
eval = case _ of
  CreateBD bd next -> do
    _ <- H.query' CP.cp1 UIBD.Slot $ H.action (UIBD.Initialize bd)
    pure next
#+END_SRC

`H.action` is a Halogen
function mapping ChildQuery constructors to concrete actions, by simply
applying the `Unit` type to it.

#+BEGIN_SRC purescript
type Action f = Unit -> f Unit
action :: forall f. Action f -> f Unit
action f = f unit
#+END_SRC

Finally, we return the next command. Next is `PropagateMessage`, which
receives a Message (sent from the function handling messages from the children):

#+BEGIN_SRC purescript
data Message
  = BDInstance Biodalliance
  | CyInstance Cytoscape
#+END_SRC

Depending on which message it is, we print a log message, and then use `H.raise`
to send the message out from Halogen to subscribers elsewhere in the app (more
on that later).

#+BEGIN_SRC purescript
  PropagateMessage msg next -> do
    case msg of
      BDInstance _ -> liftEff $ log "propagating BD"
      CyInstance _ -> liftEff $ log "propagating Cy"
    H.raise msg
    pure next
#+END_SRC

The rest are simple queries to the respective child component, practically
the same as `CreateBD`:
#+BEGIN_SRC purescript
  BDScroll dist next -> do
    _ <- H.query' CP.cp1 UIBD.Slot $ H.action (UIBD.Scroll dist)
    pure next
  BDJump chr xl xr next -> do
    _ <- H.query' CP.cp1 UIBD.Slot $ H.action (UIBD.Jump chr xl xr)
    pure next

  CreateCy div next -> do
    _ <- H.query' CP.cp2 UICy.Slot $ H.action (UICy.Initialize div)
    pure next
  ResetCy next -> do
    _ <- H.query' CP.cp2 UICy.Slot $ H.action UICy.Reset
    pure next
#+END_SRC

*** Rendering
Next is rendering the component. This is done by providing a function from
the component `state` to a description of the DSL used by the `renderer`
type. In our case, we render to `HTML`, and so use the type `ParentHTML`,
which contains all the types required to interact with the children.

#+BEGIN_SRC purescript
render :: State -> ParentHTML query childQuery childSlot m
#+END_SRC

The function itself is simple, we use Arrays and some functions to describe
the HTML tree, a simplified version follows:

#+BEGIN_SRC purescript
render _ =
  HH.div_
    [ HH.button
      [  HE.onClick (HE.input_ (BDScroll (Bp (-1000000.0)))) ]
      [ HH.text "Scroll left 1MBp" ]

    , HH.div
      [] [HH.slot' CP.cp1 UIBD.Slot UIBD.component unit handleBDMessage]
    ]
#+END_SRC

This produces a button with the text "Scroll left 1MBp", and clicking on it
sends a query to `eval` to scroll the BD view 1 MBp to the left; as well as a
div with the BD child component. Adding the child component here is how we
create the component, so we must also provide a handler in the parent for
messages from the child, namely `handleBDMessage`.

*** Messages
A component can send messages to its parent, or the rest of the application
in the case of the top-level component. These are the messages the BD and Cy.js
components can produce, respectively:

#+BEGIN_SRC purescript
data UIBD.Message
  = SendBD Biodalliance

data UICy.Output
  = SendCy Cytoscape
#+END_SRC

The main component can produce these:
#+BEGIN_SRC purescript
data Message
  = BDInstance Biodalliance
  | CyInstance Cytoscape
#+END_SRC

Note that the main container uses its own messages to propagate the children
components; message passing is limited by Halogen, and anything more complex
than this should be done on another channel (which is what GGB does with events).

The messages from the BD and Cy.js components are handled by the functions
`handleBDMessage` and `handleCyMessage`:

#+BEGIN_SRC purescript
handleBDMessage :: UIBD.Message -> Maybe (Query Unit)
handleBDMessage (UIBD.SendBD bd) = Just $ H.action $ PropagateMessage (BDInstance bd)

handleCyMessage :: UICy.Output -> Maybe (Query Unit)
handleCyMessage (UICy.SendCy cy) = Just $ H.action $ PropagateMessage (CyInstance cy)
#+END_SRC

Note that these produce Queries on the main component. We want to send the
messages containing the references to the instances out from the component to
the outside application, hence creating a PropagateMessage query wrapping the
reference. As seen in `eval` above, this in turn callse `H.raise` on the
message, sending it to the outside world.

*** Creating the component
These functions, including one to produce the initial state (simply `const unit`)
are all put together and provided to the `parentComponent` function,
producing the Component itself. This can then be provided to Halogen's `runUI`
function, along with the initial state and an HTML element to be placed in,
to create and run the Halogen component.

First, however, we need a `main` function application to run.

** The main application

`main` is the function which will be called by the user to run the browser.
It takes a `Foreign` object -- the one to parse into a browser configuration --
and then does some stuff with Eff (e.g. be a genetics browser):

-- TODO: remove row blank when compiling with 0.12
-- TODO: explain runHalogenAff
#+BEGIN_SRC purescript
main :: Foreign -> Eff _ Unit
main fConfig = HA.runHalogenAff do
#+END_SRC

First we attempt to parse the provided configuration, logging all errors to
config on failure, otherwise continuing:

#+BEGIN_SRC purescript
  case runExcept $ parseBrowserConfig fConfig of
    Left e -> liftEff $ do
      log "Invalid browser configuration:"
      sequence_ $ log <<< renderForeignError <$> e

    Right (BrowserConfig config) -> do
#+END_SRC

With a validated config, we can create the track/graph configs, and create
the function which will later be used to create Biodalliance:

#+BEGIN_SRC purescript
      let {bdTracks, cyGraphs} = validateConfigs config.tracks

          opts' = sources := bdTracks.results <>
                  renderers := config.bdRenderers

      liftEff $ log $ "BDTrack errors: " <> foldMap ((<>) ", ") bdTracks.errors
      liftEff $ log $ "CyGraph errors: " <> foldMap ((<>) ", ") cyGraphs.errors

      let mkBd :: (∀ eff. HTMLElement -> Eff (bd :: BD | eff) Biodalliance)
          mkBd = initBD opts' config.wrapRenderer config.browser
#+END_SRC

After picking the element to run in, we create the Halogen component, and
create the Buses to be used by the events system. Note that we bind the
value of `runUI` to `io`:

#+BEGIN_SRC purescript
          io <- runUI component unit el'

          busFromBD <- Bus.make
          busFromCy <- Bus.make
#+END_SRC

`io` can be used to subscribe to messages sent from the main component,
as well as send queries to it. In this case, we attach a callback
to listen for the reference to the BD instance, sent by the BD component
upon creation. We then use the TrackSink and TrackSource configurations
to hook BD up to the event system. Finally, we ask the main component
to create the BD instance:

#+BEGIN_SRC purescript
            io.subscribe $ CR.consumer $ case _ of
              BDInstance bd -> do

                case bdTrackSink of
                  Left err -> liftEff $ log "No BD TrackSink!"
                  Right ts -> forkTrackSink ts bd busFromCy *> pure unit

                liftEff $ case bdTrackSource of
                  Left err -> log err
                  Right ts -> subscribeBDEvents ts bd busFromBD

                  --TODO remove BDRef? debug stuff...
                liftEff $ setBDRef bd
                pure Nothing

              _ -> pure $ Just unit

            io.query $ H.action (CreateBD mkBd)
#+END_SRC

The Cytoscape.js code is analogous.


* TODO

Subscribes to events
  half-hard coded; should probably move to resp. halogen containers...
  describe forkTrackSink?
